WHITESPACE = _{ " " }

irc_message = {
    prefix? ~ WHITESPACE? ~ command ~ (WHITESPACE? ~ param)* ~ (WHITESPACE? ~ ":" ~ trailing_data)? ~ line_end
}

prefix = { ":" ~ source }
source = { nickname ~ ("!" ~ user_name)? ~ ("@" ~ host_name)? }

nickname          = @{ (!(invalid_nick_char) ~ ANY)+ }
invalid_nick_char = _{ "\0" | "\r" | "\n" | " " | "#" | "&" | "+" | "!" }

user_name         = @{ (!(invalid_user_char) ~ ANY)+ }
invalid_user_char = _{ "\0" | "\r" | "\n" | " " }

host_name = @{ (!(" " | "\r" | "\n") ~ ANY)+ }

command = @{ (ASCII_ALPHA+ | ASCII_DIGIT{3}) }

param = @{ (!(" " | "\r" | "\n" | ":") ~ ANY)+ }

trailing_data = @{ (!("\r" | "\n") ~ ANY)* }

line_end = { "\r\n" | "\n" }
